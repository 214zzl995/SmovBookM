plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-parcelize'
    id 'com.google.protobuf' version '0.9.0'
}

//获取local.properties中的数据
def getProperty(String filename, String propName) {
    def propsFile = rootProject.file(filename)
    if (propsFile.exists()) {
        def props = new Properties()
        props.load(new FileInputStream(propsFile))
        if (props[propName] != null) {
            return props[propName]
        } else {
            print("No such property " + propName + " in file " + filename)
        }
    } else {
        print(filename + " does not exist!")
    }
}

android {
    compileSdk versions.compileSdk

    defaultConfig {
        applicationId 'com.leri.smovbook'
        minSdk versions.minSdk
        targetSdk versions.compileSdk
        versionCode versions.versionCode
        versionName versions.versionName

        buildConfigField "String", "TMDB_API_KEY", "\"${getProperty("local.properties", "tmdb_api_key")}\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
        javaCompileOptions {
            annotationProcessorOptions {
                arguments["dagger.hilt.disableModulesHaveInstallInCheck"] = "true"
            }
        }

    }

    buildFeatures {
        compose true
        buildConfig = true
    }

    //可以通过 buildConfigField判断环境
    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            buildConfigField "boolean", "FLAG", "true"
            ndk {
                //noinspection ChromeOsAbiSupport
                abiFilters "armeabi-v7a", "x86_64", "arm64-v8a"
            }
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "FLAG", "false"
            ndk {
                //noinspection ChromeOsAbiSupport
                abiFilters "armeabi-v7a"
            }
        }
    }

    sourceSets.configureEach {
        jniLibs.srcDir("libs")
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    composeOptions {
        kotlinCompilerExtensionVersion versions.compose_version
    }
    packagingOptions {
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1}"
        }
        jniLibs {
            useLegacyPackaging true
        }
    }

    protobuf {
        protoc {
            artifact = "com.google.protobuf:protoc:3.21.4"
        }

        generateProtoTasks {
            all().each { task ->
                task.builtins {
                    java {
                        option "lite"
                    }
                }
            }
        }
    }
    namespace 'com.leri.smovbook'
}

dependencies {

    implementation "androidx.core:core-ktx:$versions.coreKtx"
    implementation "androidx.compose.ui:ui:$versions.compose_version"

    implementation "androidx.compose.ui:ui-tooling-preview:$versions.compose_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.5.1"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.7.10"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4"

    implementation "androidx.activity:activity-compose:$versions.activityCompose"
    implementation 'androidx.core:core-splashscreen:1.0.0'
    implementation "androidx.appcompat:appcompat:$versions.appcompat"

    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.5.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.5.1"

    implementation "androidx.navigation:navigation-fragment-ktx:2.5.1"
    implementation "androidx.navigation:navigation-ui-ktx:2.5.1"
    implementation "com.google.android.material:material:$versions.material"

    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1"

    implementation "androidx.compose.foundation:foundation-layout:$versions.compose_version"
    implementation "androidx.compose.material:material:$versions.compose_version"
    implementation "androidx.compose.material3:material3:1.1.0-alpha06"
    implementation "androidx.compose.material:material-icons-extended:$versions.compose_version"
    implementation "androidx.compose.ui:ui-util:$versions.compose_version"
    implementation "androidx.compose.runtime:runtime:$versions.compose_version"
    implementation "androidx.compose.runtime:runtime-livedata:$versions.compose_version"
    implementation "androidx.compose.ui:ui-viewbinding:$versions.compose_version"

    implementation "androidx.navigation:navigation-compose:$versions.nav_version"

    //image
    implementation "io.coil-kt:coil:$versions.coil"
    implementation "io.coil-kt:coil-compose:$versions.coil"

    //google
    implementation "com.google.accompanist:accompanist-flowlayout:$versions.accompanist"
    implementation "com.google.accompanist:accompanist-swiperefresh:$versions.accompanist"
    implementation "com.google.accompanist:accompanist-permissions:$versions.accompanist"
    implementation "com.google.accompanist:accompanist-systemuicontroller:$versions.accompanist"
    implementation "com.google.mlkit:barcode-scanning:17.2.0"

    implementation "androidx.datastore:datastore-preferences:1.0.0"
    implementation "androidx.datastore:datastore-core:1.0.0"
    implementation "com.google.protobuf:protobuf-javalite:3.21.5"
    implementation "androidx.datastore:datastore:1.0.0"

    implementation "com.google.guava:guava:31.1-android"

    implementation "com.airbnb.android:lottie-compose:$versions.lottie"
    implementation "com.airbnb.android:lottie:$versions.lottie"


    implementation "androidx.camera:camera-core:$versions.camerax"
    implementation "androidx.camera:camera-camera2:$versions.camerax"
    implementation "androidx.camera:camera-lifecycle:$versions.camerax"
    implementation "androidx.camera:camera-view:$versions.camerax"

    implementation "com.google.code.gson:gson:2.9.0"

    //video
    implementation 'com.github.CarGuo.GSYVideoPlayer:gsyVideoPlayer-java:v8.4.0-release-jitpack'
    implementation 'com.github.CarGuo.GSYVideoPlayer:GSYVideoPlayer-exo2:v8.4.0-release-jitpack'

    //模糊效果 因为可能带来不必要的性能开销模糊效果可以放弃
    implementation 'com.github.Dimezis:BlurView:version-2.0.2'

    //utils
    implementation 'com.blankj:utilcodex:1.31.0'

    // startup
    implementation "androidx.startup:startup-runtime:1.1.1"

    //http
    implementation "com.squareup.okhttp3:okhttp:5.0.0-alpha.6"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"
    implementation "com.github.skydoves:sandwich:1.2.7"

    //log
    implementation "com.jakewharton.timber:timber:5.0.1"


    //room
    implementation "androidx.room:room-runtime:2.4.3"
    implementation "androidx.room:room-ktx:2.4.3"
    kapt "androidx.room:room-compiler:2.4.3"

    //hilt
    implementation "com.google.dagger:hilt-android:$versions.hilt"
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0"
    kapt "com.google.dagger:hilt-compiler:$versions.hilt"
    kapt "androidx.hilt:hilt-compiler:1.0.0"
    androidTestImplementation "com.google.dagger:hilt-android-testing:$versions.hilt"
    kaptAndroidTest "com.google.dagger:hilt-compiler:$versions.hilt"

    testImplementation "junit:junit:4.13.2"

    androidTestImplementation "androidx.test.ext:junit:1.1.3"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$versions.compose_version"

    debugImplementation "androidx.compose.ui:ui-tooling:$versions.compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$versions.compose_version"

}